plugins {
    id 'org.springframework.boot' version '2.0.1.RELEASE'
    id 'com.github.samueltbrown.cucumber' version '0.9'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'org.unbroken-dome.test-sets' version '1.4.5'
}

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

dependencies {
    compile project(':api')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'io.springfox:springfox-swagger2:2.8.0'
    compile 'io.springfox:springfox-swagger-ui:2.8.0'
    compile 'org.modelmapper:modelmapper:1.1.3'

    testCompile 'org.mockito:mockito-core:2.18.3'

    integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
    integrationTestCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.3'
    integrationTestCompile 'com.github.michaelruocco:file-loader:3.0.3'

    cucumberCompile 'org.springframework.boot:spring-boot-starter-test'
    cucumberCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.3'
    cucumberCompile 'lv.ctco.cukes:cukes-rest:0.0.21'
    cucumberCompile 'info.cukes:cucumber-spring:1.2.5'
}

def environmentVariables = [:]
environmentVariables['SERVER_PORT'] = 8095
environmentVariables['MONGO_HOST'] = 'localhost'
environmentVariables['MONGO_PORT'] = 27019
environmentVariables['MONGO_DATABASE'] = 'skeleton-db'

integrationTest.doFirst {
    environmentVariables.each{ key, value ->
        environment key, value
    }
}

cucumber {
    glueDirs = ['lv.ctco.cukes']
    featureDirs = ['src/cucumber/resources/features']
    jvmOptions {
        environmentVariables.each{ key, value ->
            environment key, value
        }
    }
}