import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'org.springframework.boot' version '2.0.1.RELEASE'
    id 'com.github.samueltbrown.cucumber' version '0.9'
    id 'org.unbroken-dome.test-sets' version '1.4.5'
    id 'com.bmuschko.docker-remote-api' version '3.2.8'
    id 'com.avast.gradle.docker-compose' version '0.7.1'
}

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
    jcenter()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

dependencies {
    compile project(':api')
    compile project(':model')
    compile project(':mongo')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'io.springfox:springfox-swagger2:2.9.0'
    compile 'io.springfox:springfox-swagger-ui:2.9.0'

    testCompile 'org.mockito:mockito-core:2.18.3'

    integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
    integrationTestCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.3'
    integrationTestCompile 'com.github.michaelruocco:file-loader:3.0.3'

    cucumberCompile 'org.springframework.boot:spring-boot-starter-test'
    cucumberCompile 'lv.ctco.cukes:cukes-rest:0.0.21'
    cucumberCompile 'info.cukes:cucumber-spring:1.2.5'
}

def environmentVariables = [:]
environmentVariables['SERVER_PORT'] = 8095
environmentVariables['MONGO_HOST'] = 'localhost'
environmentVariables['MONGO_PORT'] = 27019
environmentVariables['MONGO_DATABASE'] = 'skeleton-db'

integrationTest.doFirst {
    environmentVariables.each{ key, value ->
        environment key, value
    }
}

task dockerBuildImage(type: DockerBuildImage) {
    inputDir = file('.')
    tag = 'skeleton-app:latest'
}

cucumber {
    glueDirs = ['lv.ctco.cukes']
    featureDirs = ['src/cucumber/resources/features']
    jvmOptions {
        environment 'BASE_URI', 'http://localhost:8095'
    }
}

dockerCompose.isRequiredBy(project(':app').tasks.cucumber)