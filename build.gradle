plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

allprojects {
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

subprojects {
    group 'uk.co.mruoc'
    version '1.0.0'

    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.9.1'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

// exclude test-data from aggregate coverage report as it has no tests
def coverageProjects = subprojects.findAll { it.path != ':test-data' }
task jacocoRootReport(type: JacocoReport) {
    dependsOn = coverageProjects.test
    sourceDirectories = files(coverageProjects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(coverageProjects.sourceSets.main.output)
    executionData = files(coverageProjects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
    }
}